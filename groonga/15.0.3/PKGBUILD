# Maintainer: dkc <askdkc[at]gmail.com>

pkgname=('groonga' 'groonga-doc')
pkgver=15.0.3
pkgbase=groonga
pkgrel=1
arch=('i686' 'x86_64')
url="http://groonga.org/"
license=('LGPL2.1')
source=("https://github.com/groonga/groonga/releases/download/v$pkgver/$pkgbase-$pkgver.tar.gz"
    "https://github.com/groonga/groonga/releases/download/v$pkgver/$pkgbase-$pkgver.tar.gz.asc"
    "groonga-httpd.service")
makedepends=('glib2' 'libedit' 'zeromq' 'autoconf-archive' 'libstemmer'
    'libevent' 'mecab' 'mecab-ipadic' 'msgpack-c' 'ruby')
validpgpkeys=(2701F317CFCCCB975CADE9C2624CF77434839225)

build() {
    # TODO: Enable to build arrow on AArch64/armv7h
    # TODO: grpc 1.28+ is currently causing issues to build arrow.
    # Currently, this line should be specifying to disable using arrow.
    cd $srcdir/$pkgbase-$pkgver
    ./configure --prefix=/usr \
        --localstatedir=/var \
        --sysconfdir=/etc \
        --sbindir=/usr/bin \
        --with-default-encoding=utf8 \
        --with-zlib \
        --with-lz4 \
        --disable-arrow \
        --enable-message-pack \
        --enable-shared=yes \
        --enable-static=yes \
        --with-mecab \
        --enable-mruby

    make -j$(nproc)
}

package_groonga-doc() {
    pkgdesc="Document for Groonga"
    arch=('any')

    cd $srcdir/$pkgbase-$pkgver/doc

    make DESTDIR="$pkgdir" install
}

package_groonga() {
    pkgdesc="An opensource fulltext search engine."
    depends=('glib2' 'libedit' 'zeromq' 'autoconf-archive' 'arrow' 'luajit'
        'libevent' 'mecab-ipadic' 'msgpack-c' 'ruby' 'libstemmer')
    optdepends=('cutter-test_framework' 'mercurial' 'kytea')

    cd $srcdir/$pkgbase-$pkgver
    make DESTDIR="$pkgdir" install

    # cleanup
    rm -rf "${pkgdir}/var/run"

    # delete documents
    rm -r "${pkgdir}/usr/share/doc"

    install -Dm644 ../groonga-httpd.service "$pkgdir"/usr/lib/systemd/system/groonga-httpd.service
}
sha1sums=('00c2ed1bda1e69debada0039f9ca01e5d588a3b6'
    'SKIP'
    '56b68b5ebfc6785f08ce101b263d6f56acd74d8a')
sha256sums=('2728f32f73a20a9b4bf500f11f49418c45e2842a04bebaf3dce0d038b452d114'
    'SKIP'
    '4d3f91b40b37ab473b716c6c303c1e58ca7b3f777439fc4c055be80d04ffa65b')
